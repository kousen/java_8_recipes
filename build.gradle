plugins {
    id 'java'
    id 'jacoco'
    alias(libs.plugins.versions)
    alias(libs.plugins.version.catalog.update)
    alias(libs.plugins.jmh.gradle)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacocoTestReport.dependsOn(test)

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2)
}

dependencies {
    // JUnit BOM for consistent versions
    testImplementation platform("org.junit:junit-bom:${libs.versions.junit.get()}")
    
    // JUnit bundle (includes vintage engine)
    testImplementation libs.bundles.junit
    
    // Add explicit JUnit Platform Launcher to fix version mismatch
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Mockito bundle (inline and JUnit Jupiter engine)
    testImplementation libs.bundles.mockito

    // AssertJ
    testImplementation libs.assertj

    // JMH
    jmh libs.bundles.jmh
}


